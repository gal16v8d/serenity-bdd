plugins {
	id 'java'
    // Code formatter (spotlessCheck, spotlessApply)
    id 'com.diffplug.spotless'
    // Serenity capabilities
    id 'net.serenity-bdd.serenity-gradle-plugin'
    // Dependency scan for CVE vulnerabilities
    id 'org.owasp.dependencycheck'
    // Code check
    id 'checkstyle'
}

apply from: 'gradle/checkstyle.gradle'
apply from: 'gradle/dependencyCheck/dependencyCheck.gradle'
apply from: 'gradle/spotless.gradle'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

allprojects {
    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Test).tap {
        configureEach {
            systemProperty "file.encoding", "UTF-8"
        }
    }
}

repositories {
	mavenCentral()
}

dependencies {
	annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    implementation "org.slf4j:slf4j-api:$slf4jVersion"

    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "net.serenity-bdd:serenity-junit5:$serenityVersion"
    testImplementation "net.serenity-bdd:serenity-cucumber:$serenityVersion"
    testImplementation "net.serenity-bdd:serenity-core:$serenityVersion"
    testImplementation "net.serenity-bdd:serenity-ensure:$serenityVersion"
    testImplementation "net.serenity-bdd:serenity-screenplay:$serenityVersion"
    testImplementation "net.serenity-bdd:serenity-screenplay-webdriver:$serenityVersion"
    testImplementation "org.assertj:assertj-core:$assertJVersion"
    testImplementation "ch.qos.logback:logback-classic:$logbackVersion"
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    systemProperties System.getProperties()
}

serenity {
    reports = ["single-page-html"]
    // Specify the root package of any JUnit acceptance tests
    testRoot="com.gsdd"
}

test.finalizedBy(aggregate)
